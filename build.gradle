plugins {
    id 'net.ltgt.errorprone' version '0.0.8'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '1.4'
}

def enableSimulation = hasProperty('enableSimulation')

if (!hasProperty('repo')) {
    ext.repo = 'development'
}

ext.simulationInstallDir = "$rootDir/build/install/simulation"

allprojects {
    ext.enableSimulation = enableSimulation
    ext.repo = repo

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "6.18"
        configFile = new File(rootDir, "styleguide/checkstyle.xml")
    }

    ext.armBuild = true

    // Disables doclint in java 8.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    ext.setupWpilibRepo = { publishing ->
        publishing.repositories.maven {
            url = WPILibVersion.mavenLocalUrl
        }
    }
}

apply from: 'cppSettings.gradle'

def formatArgs
if (project.hasProperty('formatArgs')) {
    if (project.formatArgs.allWhitespace) {
        formatArgs = null
    } else {
        formatArgs = project.formatArgs
    }
} else {
    formatArgs = null
}

task format(type: Exec) {
    workingDir "${rootDir}"
    if (formatArgs != null) {
        commandLine 'python', './styleguide/format.py', formatArgs
    } else {
        commandLine 'python', './styleguide/format.py'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
